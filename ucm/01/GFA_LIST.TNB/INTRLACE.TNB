' Interlace-Routine
' (c) 1993 by Lucky of ST
'
' Interlace-Richtung bestimmen: v_ilace=TRUE  => Auflsg. vertikal verdoppeln
'                                       FALSE =>   -"-   horizontal   -"-
v_ilace=FALSE
'
' Maus verstecken
'
HIDEM
'
' Aufl”sung ermitteln
'
res%=XBIOS(4)
'
' Bildschirmgr”že berechnen und in x% und y% merken
'
x%=640+320*(res%=0)
y%=400+200*(res%<2)
'
' Bildschirme einrichten
'
oscr%=XBIOS(2)
pscr%=oscr%
RESERVE FRE(0)-64290
lscr%=(HIMEM+32290) AND &HFFFF00
'
' Bildschirme l”schen
'
BMOVE oscr%,lscr%,32000
'
' N„chstbestes Degas-Pic der aktuellen Aufl”sung laden
'
BLOAD "*.pi"+STR$(res%+1),HIMEM
'
' Farben einstellen
'
~XBIOS(6,L:HIMEM+2)
'
' Beide Bilder entsprechend den Einstellungen zusammenkopieren
'
pic%=HIMEM+34
FOR a=0 TO x%-1+(x%-y%)*v_ilace
  IF v_ilace
    RC_COPY pic%,0,a,x%,1 TO pscr%-ODD(a)*(lscr%-pscr%),0,y%/4+a/2-1
  ELSE
    RC_COPY pic%,a,0,1,y% TO pscr%-ODD(a)*(lscr%-pscr%),x%/4+a/2-1,0
  ENDIF
NEXT a
'
' Routine zum Vertauschen der beiden Bildschirme. Wird bis zu einem Tasten-
' druck immer wieder wiederholt.
'
REPEAT
  SWAP lscr%,pscr%
  ~XBIOS(5,L:lscr%,L:pscr%,-1)
  VSYNC
UNTIL INKEY$<>""
'
' Original-Bildschirm benutzen
'
~XBIOS(5,L:oscr%,L:oscr%,-1)
'
' Degas-Pic in normaler Gr”že darstellen
'
BMOVE pic%,oscr%,32000
'
' Auf Tastendruck warten
'
~INP(2)
'
' Speicher (von zweitem Bildschirm...) freigeben
'
RESERVE
'
' Maus an und Programmende...
'
SHOWM
