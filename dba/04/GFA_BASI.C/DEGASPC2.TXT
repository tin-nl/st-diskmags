DEGAS.PC2 UNPACKER

Easy does it,

you could have figured this one out for yourself cause there isn't that
much difference between the PC1 and PC2 unpacker. The main difference is the
fact that Med resolution has only 2 planes and Low resolution has 4 planes.

A small explaination about planes:

4 planes well you could see them as 4 layers (screens) laying behind each  
other, to say it in other words they lay on top of one another.

   plane 4           .     
   plane 3          .   al together => . 1 pixel with 4 planes   
   plane 2         .         
   plane 1        .   
                 

For example planes 1 has a yellow color and plane 2 has a bleu color then
the mix of plane 1 and 2 will give you the colour green. Its quite easy.
And thats why there are 16 colours in low resolution, 4 planes with each
4 options = 4*4=16 colours.

Asfar as med resolution is, there are only 2 planes, so 2 planes with each
2 options = 2*2=4 colours. In the listing you can find the planes part
'FOR plane%=0 TO 2 STEP 2' and this part is to select the plane to be set.
But oke enough of this,this listing you can find unter the name "UNPAKMED.LST"
and MERGE it in GFA basic 3.xx.
Enjoy,

sc%=XBIOS(3)                                    ! start address screen
'
DO
  SHOWM                                         ! showm mouse
  FILESELECT "a:\pictures\*.pc2","default.pc1",a$  ! fileselector to choose pic.
  IF a$<>""                                     ! if exists
    HIDEM                                       ! hide mouse
    OPEN "i",#1,a$                              ! open picture
    dummy$=INPUT$(2,#1)                         ! read first 2 usless (to us)
    '                                           ! bytes.
    FOR i%=0 TO 3                               ! for next to get the 4 colors
      c%=256*ASC(INPUT$(1,#1))+ASC(INPUT$(1,#1))! make decimal value of it
      SETCOLOR i%,c%                            ! setcolor to the value
    NEXT i%                                     ! get next color
    dummy$=INPUT$(24,#1)                        ! read rest of usless colors
    scr%=sc%                                    ! copy screen address in scr%
    FOR scan%=0 TO 199                          ! for next for number of lines
      FOR plane%=0 TO 2 STEP 2                  ! for next for number of bit
        '                                       ! planes ( med res 2 planes )
        addr%=scr%+160*scan%+plane%             ! screen counter
        volg%=addr%+160                         ! screen counter + 1 line
        DO                                      ! start unpacking
          b%=ASC(INPUT$(1,#1))
          IF b%<128
            FOR i%=0 TO b%
              POKE addr%,ASC(INPUT$(1,#1))
              IF addr% AND 1
                addr%=addr%+3
              ELSE
                INC addr%
              ENDIF
            NEXT i%
          ENDIF
          IF b%>128
            tel%=ASC(INPUT$(1,#1))
            FOR i%=1 TO 256-b%+1
              POKE addr%,tel%
              IF addr% AND 1
                addr%=addr%+3
              ELSE
                INC addr%
              ENDIF
            NEXT i%
          ENDIF
          EXIT IF addr%=volg%
        LOOP
      NEXT plane%
    NEXT scan%
    CLOSE #1
    OUT 2,7
    ~INP(2)
    CLS
    SETCOLOR 0,0
    SETCOLOR 15,7,7,7
  ELSE
    END
  ENDIF
LOOP

For more comment see the UNPAKLOW.LST file.

                                        Bonus Software.
