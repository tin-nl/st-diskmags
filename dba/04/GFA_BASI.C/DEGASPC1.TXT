DEGAS.PC1 UNPACKER

How to unpack PC1 pictures in GFA Basic ???

So here is a little routine for the new GFA programmers that like to use
Degas pictures in there programs. I know that a normal unpacked picture in 
Degas eats up a lot of precious diskspace so the made an option in Degas
and Degas Elite to save the pictures as a compressed file. Thats nice
but there is still the small matter of how to unpack these pictures again.
In DBA diskmagazine 1, we showed how to load a normal picture, and here is
a new one of how to load and unpack PC1 pictures so you can save lot's of
diskspace. This isn't the fastest routine to unpack pictures but you could
alter it yourself so you can unpack pictures you have INLINE in your 
program. Because it's to easy to do everything for you, just try it out for
yourself, if there are to many questions send in by the next issue i even
may help you out. And if you are writing, send some of your own small 
routines, so everyone can enjoy it....

Oke here comes the listing, its also on disk in the folder GFA_PROGS unter
the name UNPAKLOW.LST and you can use it again in a version of GFA 3.00
and up...
 '
sc%=XBIOS(3)                                    ! start address screen
'
DO
  SHOWM                                         ! showm mouse
  FILESELECT "a:\pictures\*.pc1","default.pc1",a$  ! fileselector to choose pic.
  IF a$<>""                                     ! if exists
    HIDEM                                       ! hide mouse
    OPEN "i",#1,a$                              ! open picture
    dummy$=INPUT$(2,#1)                         ! read first 2 usless (to us)
    '                                           ! bytes.
    FOR i%=0 TO 15                              ! for next to get the 16 colors
      c%=256*ASC(INPUT$(1,#1))+ASC(INPUT$(1,#1))! make decimal value of it
      SETCOLOR i%,c%                            ! setcolor to the value
    NEXT i%                                     ! get next color
    scr%=sc%                                    ! copy screen address in scr%
    FOR scan%=0 TO 199                          ! for next for number of lines
      FOR plane%=0 TO 6 STEP 2                  ! for next for number of bit
        ~INP(2)
        '                                       ! planes ( low res 4 planes )
        addr%=scr%+160*scan%+plane%             ! screen counter
        volg%=addr%+160                         ! screen counter + 1 line
        DO                                      ! start unpacking
          b%=ASC(INPUT$(1,#1))                  ! get byte
          IF b%<128                             ! if it is smaller then 128
            FOR i%=0 TO b%                      !
              POKE addr%,ASC(INPUT$(1,#1))      ! poke it straight into the
              '                                 ! screen
              IF addr% AND 1
                addr%=addr%+7
              ELSE
                INC addr%
              ENDIF
            NEXT i%
          ENDIF
          IF b%>128                             ! if bigger then 128
            tel%=ASC(INPUT$(1,#1))              ! get next byte for signed
            '                                   ! single byte number
            FOR i%=1 TO 256-b%+1                ! subtract it from 256 to know
              '                                 ! the signed one.
              POKE addr%,tel%                   ! poke into screen
              IF addr% AND 1
                addr%=addr%+7
              ELSE
                INC addr%
              ENDIF
            NEXT i%
          ENDIF
          EXIT IF addr%=volg%                   !exit if end of line
        LOOP
      NEXT plane%                               ! next plane in line
    NEXT scan%                                  ! next line
    CLOSE #1                                    ! if end of file close #1
    OUT 2,7                                     ! make ping sound to know if
    '                                           ! picture is ready
    ~INP(2)                                     ! wait for key press
    CLS                                         ! clear screen
    SETCOLOR 0,0                                ! black background
    SETCOLOR 15,7,7,7                           ! white letters
  ELSE
    END                                         ! if no filename is chosen
    '                                           ! then just end program
  ENDIF
LOOP

Yep thats all, isn't to large isn't it. Well i hope you can use it, and if
you use it in a small demo, just credit the DBA Magazine oke ??
Until next time,

                                        Bonus Software,

                                   ( may the GFA be with you )


